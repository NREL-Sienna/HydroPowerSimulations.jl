{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Operation Problem with `HydroPowerSimulations.jl` using Market Bid Cost\n",
    "\n",
    "> **Set up**\n",
    ">\n",
    "> To run this notebook, first install the Julia kernel for Jupyter Notebooks using [IJulia](https://julialang.github.io/IJulia.jl/stable/manual/installation/), then [create an environment](https://pkgdocs.julialang.org/v1/environments/) for this tutorial with the packages listed with `using <PackageName>` further down.\n",
    ">\n",
    "> This tutorial has demonstrated compatibility with these package versions. If you run into any errors, first check your package versions for consistency using `Pkg.status()`.\n",
    ">\n",
    " > ```\n",
    " > Status `~/work/HydroPowerSimulations.jl/HydroPowerSimulations.jl/docs/Project.toml`\n",
    " >   [a93c6f00] DataFrames v1.8.1\n",
    " >   [864edb3b] DataStructures v0.19.3\n",
    " >   [e30172f5] Documenter v1.16.1\n",
    " >   [d12716ef] DocumenterInterLinks v1.1.0\n",
    " >   [35a29f4d] DocumenterTools v0.1.21\n",
    " >   [87dc4568] HiGHS v1.21.0\n",
    " >   [fc1677e0] HydroPowerSimulations v0.13.1 `~/work/HydroPowerSimulations.jl/HydroPowerSimulations.jl`\n",
    " >   [2cd47ed4] InfrastructureSystems v3.3.0\n",
    " >   [b6b21f68] Ipopt v1.14.0\n",
    " >   [23fbe1c1] Latexify v0.16.10\n",
    " >   [98b081ad] Literate v2.21.0\n",
    " > ⌅ [94fada2c] PowerFlows v0.13.1\n",
    " >   [e690365d] PowerSimulations v0.32.4\n",
    " >   [f00506e0] PowerSystemCaseBuilder v2.2.0\n",
    " >   [bcd98974] PowerSystems v5.4.0\n",
    " >   [08abe8d2] PrettyTables v3.1.2\n",
    " >   [9e3dc215] TimeSeries v0.25.2\n",
    " > Info Packages marked with ⌅ have new versions available but compatibility constraints restrict them from upgrading. To see why use `status --outdated`\n",
    " > \n",
    " > ```\n",
    "\n",
    "\n",
    "> *Note*\n",
    ">\n",
    ">\n",
    "> `HydroPowerSimulations.jl` is an extension library of [`PowerSimulations.jl`](https://nrel-sienna.github.io/PowerSimulations.jl/latest/) for modeling hydro units. Users are encouraged to review the [single-step tutorial in `PowerSimulations.jl`](https://nrel-sienna.github.io/PowerSimulations.jl/latest/tutorials/decision_problem/) before this tutorial.\n",
    ">\n",
    "## Load packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using PowerSystems\n",
    "using PowerSimulations\n",
    "using HydroPowerSimulations\n",
    "using PowerSystemCaseBuilder\n",
    "using HiGHS ## solver\n",
    "using TimeSeries\n",
    "using Dates"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Data\n",
    "\n",
    "> *Note*\n",
    ">\n",
    ">\n",
    "> `PowerSystemCaseBuilder.jl` is a helper library that makes it easier to reproduce examples in the documentation and tutorials. Normally you would pass your local files to create the system data instead of calling the function `build_system`.\n",
    "> For more details visit [PowerSystemCaseBuilder README](https://github.com/NREL-Sienna/PowerSystemCaseBuilder.jl/blob/main/README.md)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sys = build_system(PSITestSystems, \"c_sys5_hy\"; add_single_time_series = true)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Add a time-varying fuel cost for a thermal unit\n",
    "\n",
    "We will modify the cheapest unit Brighton to have time-varying fuel cost. First we add a `PowerSystems.FuelCurve`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "brighton = get_component(ThermalStandard, sys, \"Brighton\")\n",
    "old_thermal_cost = get_operation_cost(brighton)\n",
    "new_fuel_curve = FuelCurve(;\n",
    "    value_curve = LinearCurve(8.0), ## Typical plant of 8 MMBTU/MWh heat rate. Piecewise heat rates can be used if needed.\n",
    "    power_units = UnitSystem.NATURAL_UNITS,\n",
    "    fuel_cost = 1.0, ## $/MMBTU default fuel cost to start\n",
    ")\n",
    "new_thermal_cost = ThermalGenerationCost(;\n",
    "    variable = new_fuel_curve,\n",
    "    fixed = old_thermal_cost.fixed,\n",
    "    start_up = old_thermal_cost.start_up,\n",
    "    shut_down = old_thermal_cost.shut_down,\n",
    ")\n",
    "set_operation_cost!(brighton, new_thermal_cost)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we create a timeseries with random fuel prices. We first grab an existing timeseries:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "existing_ts = get_time_series_array(\n",
    "    SingleTimeSeries,\n",
    "    first(get_components(PowerLoad, sys)),\n",
    "    \"max_active_power\",\n",
    ")\n",
    "tstamps = timestamp(existing_ts)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And add the timeseries with the `set_fuel_cost!` method."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "fuel_cost_values = rand(length(tstamps)) .+ 1.0 ## Random fuel cost between 1.0 and 2.0 $/MMBTU\n",
    "fuel_cost_tarray = TimeArray(tstamps, fuel_cost_values)\n",
    "fuel_cost_ts = SingleTimeSeries(; name = \"fuel_cost\", data = fuel_cost_tarray)\n",
    "set_fuel_cost!(sys, brighton, fuel_cost_ts)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Add a market bid cost to the hydro unit\n",
    "\n",
    "We again grab the timestamps from an existing time series:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "existing_ts = get_time_series_array(\n",
    "    SingleTimeSeries,\n",
    "    first(get_components(PowerLoad, sys)),\n",
    "    \"max_active_power\",\n",
    ")\n",
    "tstamps = timestamp(existing_ts)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And we add an empty market bid cost to the hydro:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "hy = get_component(HydroDispatch, sys, \"HydroDispatch\")\n",
    "# Create an empty market bid and set it\n",
    "hydro_cost = MarketBidCost(;\n",
    "    no_load_cost = 0.0,\n",
    "    start_up = (hot = 0.0, warm = 0.0, cold = 0.0),\n",
    "    shut_down = 0.0,\n",
    ")\n",
    "set_operation_cost!(hy, hydro_cost)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we create a time-varying piecewise linear bid cost:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "psd1 = PiecewiseStepData([0.0, 600.0], [5.0])\n",
    "psd2 = PiecewiseStepData([0.0, 300.0, 600.0], [10.0, 20.0])\n",
    "psd3 = PiecewiseStepData([0.0, 600.0], [500.0])\n",
    "\n",
    "# Cheap the first 10 hours, moderate next 4 hours, expensive last 34 hours\n",
    "total_step_data = vcat([psd1 for x in 1:10], [psd2 for x in 1:4], [psd3 for x in 1:34])\n",
    "mbid_tarray = TimeArray(tstamps, total_step_data)\n",
    "ts_mbid = SingleTimeSeries(; name = \"variable_cost\", data = mbid_tarray)\n",
    "\n",
    "set_variable_cost!(sys, hy, ts_mbid, UnitSystem.NATURAL_UNITS)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "It is also needed to create the initial input time series for market bid. That is the cost at 0 power at each time step. We will use zero for this example."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "zero_input = zeros(length(tstamps))\n",
    "zero_tarray = TimeArray(tstamps, zero_input)\n",
    "ts_zero = SingleTimeSeries(; name = \"variable_cost_initial_input\", data = zero_tarray)\n",
    "set_incremental_initial_input!(sys, hy, ts_zero)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Running the single-stage problem\n",
    "\n",
    "We first transform the single time series to a 24 hour forecast"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "transform_single_time_series!(sys, Hour(24), Hour(24))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And create the necessary templates for the system:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "template = ProblemTemplate(\n",
    "    NetworkModel(\n",
    "        CopperPlatePowerModel;\n",
    "        use_slacks = true,\n",
    "        duals = [CopperPlateBalanceConstraint],\n",
    "    ),\n",
    ")\n",
    "set_device_model!(template, ThermalStandard, ThermalBasicUnitCommitment)\n",
    "set_device_model!(template, HydroDispatch, HydroDispatchRunOfRiver)\n",
    "set_device_model!(template, PowerLoad, StaticPowerLoad)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And then running the decision model:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model = DecisionModel(\n",
    "    template,\n",
    "    sys;\n",
    "    name = \"UC_MBCost\",\n",
    "    optimizer = optimizer_with_attributes(\n",
    "        HiGHS.Optimizer,\n",
    "    ),\n",
    "    store_variable_names = true,\n",
    "    optimizer_solve_log_print = false,\n",
    ")\n",
    "build!(model; output_dir = mktempdir())\n",
    "solve!(model)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And exploring results we confirm that the hydro is not dispatched when is more expensive, while the dual of the CopperPlate constraint showcase that the system become more expensive at those hours."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "res = OptimizationProblemResults(model)\n",
    "hy_p = read_variable(\n",
    "    res,\n",
    "    \"ActivePowerVariable__HydroDispatch\";\n",
    "    table_format = TableFormat.WIDE,\n",
    ");\n",
    "show(hy_p; allrows = true)\n",
    "\n",
    "dual_price =\n",
    "    read_dual(res, \"CopperPlateBalanceConstraint__System\"; table_format = TableFormat.WIDE);\n",
    "show(dual_price; allrows = true)"
   ],
   "metadata": {},
   "execution_count": null
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
